#version 450 core

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

struct TrackSegment {
    vec4 start_r; // start.xyz, radius in .w
    vec4 end_pad; // end.xyz, unused in .w
};

layout(std430, binding = 0) buffer CollisionOutput {
    float terrainHeight;
};

// UBO set in ChunkManager
layout(std140, binding = 3) uniform TerrainParams {
    float u_bedrockFrequency;
    float u_bedrockAmplitude;
    float u_frequency;
    float u_frequencyMultiplier;
    float u_amplitude;
    float u_amplitudeMultiplier;
	int u_octaves;
    float u_floorLevel;
    float u_blendFactor;
    float u_warpFreq;
    float u_warpAmp;
    float u_warpFreqMult;
    float u_warpAmpMult; 
    int u_warpOctaves;
    int u_seed;
    float u_waterLevel;
};


uniform vec3 pos;
uniform float maxHeight;

float isolevel = 0.0;

// ---------- Seed ----------
vec3 seedOffset(int s) {
    return vec3(
        float(s) * 127.1 + 311.7,
        float(s) * 269.5 + 183.3,
        float(s) * 419.2 + 247.0
    );
}

// ---------- Noise ----------
vec3 random3(vec3 c) {
	float j = 4096.0*sin(dot(c,vec3(17.0, 59.4, 15.0)));
	vec3 r;
	r.z = fract(512.0*j);
	j *= .125;
	r.x = fract(512.0*j);
	j *= .125;
	r.y = fract(512.0*j);
	return r-0.5;
}

// Skew constants for 3d simplex functions
const float F3 =  0.3333333;
const float G3 =  0.1666667;
float simplex3d(vec3 p) {
	 vec3 s = floor(p + dot(p, vec3(F3)));
	 vec3 x = p - s + dot(s, vec3(G3));
	 vec3 e = step(vec3(0.0), x - x.yzx);
	 vec3 i1 = e*(1.0 - e.zxy);
	 vec3 i2 = 1.0 - e.zxy*(1.0 - e);
	 vec3 x1 = x - i1 + G3;
	 vec3 x2 = x - i2 + 2.0*G3;
	 vec3 x3 = x - 1.0 + 3.0*G3;
	 vec4 w, d;
	 w.x = dot(x, x);
	 w.y = dot(x1, x1);
	 w.z = dot(x2, x2);
	 w.w = dot(x3, x3);
	 w = max(0.6 - w, 0.0);
	 d.x = dot(random3(s), x);
	 d.y = dot(random3(s + i1), x1);
	 d.z = dot(random3(s + i2), x2);
	 d.w = dot(random3(s + 1.0), x3);
	 w *= w;
	 w *= w;
	 d *= w;

	 return dot(d, vec4(52.0));
}

float fbmSimplex3D(vec3 p, float freq, float amp, float fMul, float aMul, int octs) {
    p += seedOffset(u_seed);

    float acc = 0.0;
    for (int i = 0; i < octs; ++i) {
        acc += simplex3d(p * freq) * amp;
        freq *= fMul;
        amp  *= aMul;
    }
    return acc;
}


// ---------- Terrain density ----------
float densityAt(vec3 pos) {    
    // Bedrock
    float bedrockNoise = fbmSimplex3D(vec3(pos.x, 0.0, pos.z), u_bedrockFrequency, u_bedrockAmplitude, u_frequencyMultiplier, u_amplitudeMultiplier, u_octaves);
    float bedrockDensity = -pos.y + bedrockNoise + u_floorLevel;

    return bedrockDensity;
}

void main() {
    terrainHeight = 0.0;
    if(densityAt(pos) > isolevel) return;

    vec3 rayOrigin = pos;
    vec3 rayDirection = vec3(0.0, -1.0, 0.0); // ray pointing down
    float stepSize = 1.0;
    float distanceTravelled = 0.0;
    bool hit = false;

    while(distanceTravelled < maxHeight) {
        vec3 currentPosition = rayOrigin + distanceTravelled * rayDirection;
        float terrainIso = densityAt(currentPosition);
        if (terrainIso >= isolevel) {
            hit = true;
            break;
        }

        distanceTravelled += stepSize;
    }

    if (hit) terrainHeight = maxHeight - distanceTravelled;
}